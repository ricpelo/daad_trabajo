; GFX, VGA and palette support testing for DAAD
;
#include c:\daad\symbols.sce
/CTL    ;Control Section
_       ;Null word character is an underline
/TOK    ;Tokens as supplied with PAW under CP/M
_the_
_you_
_are_
ing_
_to_
_and
_is_
You_
and_
The_
n't_
_of_
_you
ing
ed_
_a_
_op
ith
out
ent
_to
_in
all
_th
_it
ter
ave
_be
ver
her
and
ear
You
_on
en_
ose
no
ic
ap
_b
gh
__
ad
is
_c
ir
ay
ur
un
oo
_d
lo
ro
ac
se
ri
li
ti
om
bl
ck
I_
ed
ee
_f
ha
pe
e_
t_
in
s_
th
,_
er
d_
on
to
an
ar
en
ou
or
st
._
ow
le
at
al
re
y_
ch
am
el
_w
as
es
it
_s
ll
do
op
sh
me
he
bo
hi
ca
pl
il
cl
_a
of
_h
tt
mo
ke
ve
so
e.
d.
t.
vi
ly
id
sc
_p
em
r_
;       -       -       -       -       -       -       -       -       -
/VOC    ;Vocabulary
;                       Movements ie verbs and nouns < 14
N       2       noun
NORTH   2       noun
S       3       noun
SOUTH   3       noun
E       4       noun
EAST    4       noun
W       5       noun
WEST    5       noun
NE      6       noun
NW      7       noun
SE      8       noun
SW      9       noun
U       10      noun
UP      10      noun
ASCEN   10      verb
D       11      noun
DOWN    11      noun
DESCE   11      verb
;                       Nouns   <20 means can be used as verbs
;                               <50 means a proper noun ie not an 'IT'
I       14      noun
INVEN   14      noun
ALL     20      noun
LOT     20      noun
EVERY   20      noun
;                       Verbs
GET     20      verb
TAKE    20      verb
DROP    21      verb
PUT     21      verb
REMOV   22      verb
WEAR    23      verb
R       24      verb
REDES   24      verb
QUIT    25      verb
STOP    25      verb
SAVE    26      verb
LOAD    27      verb
RAMSA   28      verb
RAMLO   29      verb
LOOK    30      verb
EXAMI   30      verb
SAY     31      verb
ASK     31      verb
TALK    31      verb
SPEAK   31      verb
;
P       32      verb
;
0       50      noun
1       51      noun
2       52      noun
3       53      noun
4       54      noun
5       55      noun
6       56      noun
7       57      noun
8       58      noun
9       59      noun
10      60      noun
11      61      noun
12      62      noun
13      63      noun
14      64      noun
15      65      noun
16      66      noun
17      67      noun
100     70      noun
150     71      noun
200     72      noun
250     73      noun
;                               Adjectives
SMALL   2       adjective
BIG     3       adjective
LARGE   3       adjective
OLD     4       adjective
NEW     5       adjective
HARD    6       adjective
SOFT    7       adjective
SHORT   8       adjective
LONG    9       adjective
;                               Adverbs
QUICK   2       adverb
SLOWL   3       adverb
QUIET   4       adverb
LOUDL   5       adverb
CAREF   6       adverb
SOFTL   6       adverb
GENTL   6       adverb
;                               Prepositions
TO      2       preposition
FROM    3       preposition
IN      4       preposition
OUT     5       preposition
THROU   6       preposition
OVER    7       preposition
UNDER   8       preposition
BY      9       preposition
ON      10      preposition
OFF     11      preposition
AT      12      preposition
EXCEP   13      preposition
;                               Pronouns
IT      2       pronoun
THEM    2       pronoun
;                               Conjugations
AND     2       conjugation
THEN    2       conjugation
;
; Colour setters
;
T       33      Verb
F       34      Verb
X       35      verb
Y       36      verb
;
R1      1       Adjective
R31     32      Adjective
R63     64      Adjective
R95     96      Adjective
R127    128     Adjective
R159    159     Adjective
R191    191     Adjective
R223    223     Adjective
R254    254     Adjective
G1      1       Noun
G31     32      Noun
G63     64      Noun
G95     96      Noun
G127    128     Noun
G159    159     Noun
G191    191     Noun
G223    223     Noun
G254    254     Noun
B1      1       Adjective
B31     32      Adjective
B63     64      Adjective
B95     96      Adjective
B127    128     Adjective
B159    159     Adjective
B191    191     Adjective
B223    223     Adjective
B254    254     Adjective
;       -       -       -       -       -       -       -       -       -
/STX    ;System Message Texts
/0
It's too dark to see anything.
/1
I can also see: 
/2

What now?
/3

What next?
/4

What should I do now?
/5

What should I do next?
/6
I was not able to understand any of that.  Please try again.

/7
I can't go in that direction.

/8
I can't do that.

/9
I have with me:

/10
I am wearing:

/11 ;*Spare
/12
Are you sure? 
/13
Would you like another go? 
/14 ;*Spare
/15
OK.

/16
Press any key to continue.

/17 ;*You have taken
/18 ;*\sturn
/19 ;*s
/20 ;*.[CR]
/21 ;*You have scored
/22 ;*%[CR]
/23
I'm not wearing one of those.

/24
I can't.  I'm wearing the _.

/25
I already have the _.

/26
There isn't one of those here.

/27
I can't carry any more things.

/28
I don't have one of those.

/29
I'm already wearing the _.

/30     ;One upper case character only
Y
/31     ;One upper case character only
N
/32
More...
/33

>
/34 ;*Spare
/35

Time passes...

/36
I now have the _.

/37
I'm now wearing the _.

/38
I've removed the _.

/39
I've dropped the _.

/40
I can't wear the _.

/41
I can't remove the _.

/42
I can't remove the _.  My hands are full.

/43
The _ weighs too much for me.

/44
The _ is in the 
/45
The _ isn't in the 
/46
, 
/47
 and 
/48
.

/49
I don't have the _.

/50
I'm not wearing the _.

/51
.

/52
There isn't one of those in the 
/53
Nothing.

/54 ;Letter for Tape
T
/55 ;Disc
D
/56
Drive not ready - press any key to retry.

/57
I/O Error.

/58
Disc or Directory may be full.

/59
Invalid filename.

/60
Type in name of file:
/61
Start tape.

/62
Tape or Disc?
;       -       -       -       -       -       -       -       -       -
/MTX    ;Message Texts
/0
Register:\s
/1
Red:\s
/2
Green:\s
/3
Blue:\s
/4
No palette to fade mate...
;       -       -       -       -       -       -       -       -       -
/OTX    ;Object Texts
/0
A pocket torch. (lit)
;       -       -       -       -       -       -       -       -       -
/LTX    ;Location Texts
/0
DAAD Version 2.argggg test file.

With 'P' for pause testing

'T reg Rx Gx Bx' (where x=0,31,63,95,127,159,191,223,255 and reg=0-15) to
change a palette register.

'R reg' to see contents of a palette register

'F' for a fade

'X' does some things that proves GFX...

/1
I am on a small raft floating in open sea.  The bright yellow sun is starting
to burn my skin and ominous dark fins circle slowly in the clear blue water.
/2
/3
/4
/5
/6
/7
/8
/9
/10
Pictures galore, keep going north...
/11
/12
/13
/14
/15
/16
/17
/18
/19
/20
/21
/22
/23
;       -       -       -       -       -       -       -       -       -
/CON    ;Connections
/0
/1
N 10
/2
/3
/4
/5
/6
/7
/8
/9
/10
N 18
/11
/12
/13
/14
/15
/16
/17
/18
N 19
/19
N 20
/20
N 21
/21
N 22
/22
N 23
/23
N 1
;       -       -       -       -       -       -       -       -       -
/OBJ    ;Object Definitions
;obj  starts  weight    c w  5 4 3 2 1 0 9 8 7 6 5 4 3 2 1 0    noun   adjective
;num    at
/0      CARRIED 1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    _       _
;       -       -       -       -       -       -       -       -       -
/PRO 0       ;Main Location Loop

_       _       AT      0               ; Starting game
                PROCESS 6               ; then we need init sequence

_       _       WINDOW  0               ; Select graphics window
                CLEAR   DarkF           ; Assume light
;                MINUS   2       1       ; Decrement flag 2    (See notes)
                NOTZERO 0
;                MINUS   3       1       ; & Flag 3 if Dark    (See notes)
                ABSENT  0
;                MINUS   4       1       ; & Flag 4 if No Lamp (See notes)
                SET     DarkF           ; Dark

_       _       PICTURE [Player]        ; If there is a picture, Load it
                DISPLAY [DarkF]         ; & Display it if not dark, else CLS

_       _       WINDOW  1
                NOTZERO DarkF           ; Dark
                SYSMESS 0

_       _       ZERO    DarkF
                DESC    [Player]        ; Doesn't exit loop now

_       _       PROCESS 3
; Now we use Process 1 as the main code loop, a return from it is the end
; of the game

_       _       PROCESS 1

;       -       -       -       -       -       -       -       -       -
/PRO 1
_       _       PROCESS 4               ; Do process 2 stuff here

;_       _       MINUS   5       1       ; Update auto flags
;                MINUS   6       1       ; These are not supported anymore
;                MINUS   7       1       ; as we use the low section as
;                MINUS   8       1       ; a small stack
;                NOTZERO 0               ; But should you need them
;                MINUS   9       1       ; this is the Version 1 DAAD code!
;                ABSENT  0
;                MINUS   10      1

_       _       PARSE   0               ; Get next LS from current buffer
                PROCESS 2               ; Failed cos of invalid or timeout
                REDO

_       _       EQ      Turns   255     ; Max for one byte
                PLUS    Turns+1 1
                CLEAR   Turns
                SKIP    1

_       _       PLUS    Turns   1

_       _       PROCESS 5               ; Do any commands
                ISDONE                  ; Done something
                REDO

_       _       MOVE    Player          ; No so try to move player
                RESTART                 ; Absolute jump to start process 0

_       _       NEWTEXT
                LT      Verb    14
                SYSMESS 7
                REDO

_       _       SYSMESS 8
                REDO

;       -       -       -       -       -       -       -       -       -

/PRO 2
_       _       HASAT TIMEOUT
                SYSMESS 35
                DONE

_       _       SYSMESS 6               ; I didn't understand

/PRO 3 ; Old process 1
; Old process one processing is here, this is better carried out thus

_       _       NEWLINE
                ZERO    DarkF            ; Isn't dark
                LISTOBJ

;than the old system needed in Version 1, without accesss to DarkF
;_       _       NEWLINE
;                ZERO    0               ;If it is light...
;                ABSENT  0               ;and the light source is absent...
;                LISTOBJ                 ;List the objects

;_       _       PRESENT 0               ;If the light source is present...
;                LISTOBJ                 ;List the objects

/PRO 4 ; Old process 2

/PRO 5 ; Command decoder


;Test the GFX write and swap functions. I.e. to ensure palette changes
;correctly and clears operate

X       _       COPYFF  CurWin 100
                WINDOW  0
                GFX 0 1 ; Copy phys-back
                GFX 0 4 ; write to back
                PICTURE 18 ; Load a different picture
                DISPLAY 0 ; Do it
                GFX 0 2 ; Look at it
                ANYKEY
                GFX 0 5 ; Clear physical
                ANYKEY
                GFX 0 2 ; Restore
                GFX 0 3 ; Write to phys
                ANYKEY
                GFX 0 6 ; Clear the back
                GFX 0 0 ; Set up back as us now (ie. CLS we hope)
                WINDOW [100]
                OK

;Test the VGA palette setter

#if PC
 F       _       LT      ScMode 128
                 MESSAGE 4
                 DONE
#endif

F       _       PROCESS 9
                OK

T       _       COPYFF  Noun1 100       ; Get palette no!
                MINUS   100 50
                COPYFF  Adject1 101
                COPYFF  Noun2 102
                COPYFF  Adject2 103
                GFX     100 9           ; Change palette
                OK

R       _       COPYFF  Noun1 100
                MINUS   100 50
                PROCESS 8
                OK

P       _       GT      Noun1   49
                LT      Noun1   61
                COPYFF  Noun1 Work1
                MINUS   Work1 50
                PROCESS 7
                OK

P       100     LET     Work1 100
                PROCESS 7
                OK

P       150     LET     Work1 150
                PROCESS 7
                OK

P       200     LET     Work1 200
                PROCESS 7
                OK

P       250     LET     Work1 250
                PROCESS 7
                OK

I       _       SYSMESS 9
                LISTAT  CARRIED
                SYSMESS 10
                LISTAT  WORN
                DONE

GET     ALL     DOALL   HERE
GET     _       AUTOG
                DONE

DROP    ALL     DOALL   CARRIED
DROP    _       AUTOD
                DONE

REMOVE  ALL     DOALL   WORN
REMOVE  _       AUTOR
                DONE

WEAR    ALL     DOALL   CARRIED
WEAR    _       AUTOW
                DONE

R       _       RESTART

QUIT    _       QUIT
                END

QUIT    _       DONE                    ;Not like QUIT on PAW

SAVE    _       SAVE    0               ;ditto
                RESTART

LOAD    _       LOAD    0
                RESTART

RAMSA   _       RAMSAVE
                RESTART

RAMLO   _       RAMLOAD 255             ;Reload all flags
                RESTART

LOOK    _       RESTART

/PRO 6 ; Initialise the DAAD system

_       _       WINDOW  1               ; Windows are random
                PICTURE 0        ; Need a fixed pic to set the palette right
                DISPLAY 0        ; and the templos .dat file has one here

_       _       WINAT   14       0
                WINSIZE 25      127     ; Maximum window
                CLS
                DESC    0               ; Introduction
                ANYKEY
                CLS
                CLEAR   255             ; Clear all flags

_       _       NOTEQ   255     GFlags
                CLEAR   [255]

_       _       PLUS    255     1
                LT      255     255     ; Will be set at end to indicate init
                SKIP    -1              ; has been done once

_       _       RESET                   ; Set objects to start location & Flag 1
                LET     Strength 10
                LET     MaxCarr  4
                SET     CPNoun
                SET     CPAdject
                GOTO    1               ; Main game

/PRO 7  ; Test pause values (run 50 times for security)

_       _       LET     Work2 50

$Skip
_       _       PAUSE   [Work1]
                MINUS   Work2 1
                NOTZERO Work2
                SKIP    $Skip

/PRO 8  ; Read out RGB for register in Flag 100

_       _       GFX     100 10
                MES     0
                PRINT   100
                NEWLINE
                MES     1
                PRINT   101
                NEWLINE
                MES     2
                PRINT   102
                NEWLINE
                MES     3
                PRINT   103
                NEWLINE

/PRO 9 ; Fade to black!

_       _       LET     100 15          ; Get current RGBs of Colour 15
                GFX     100 10
                COPYFF  101 104         ; Save em
                COPYFF  102 105
                COPYFF  103 106

$FADE
_       _       ZERO    101             ; RED at 0?
                ANYKEY
                COPYFF  104 101
                COPYFF  105 102
                COPYFF  106 103
                GFX     100 9           ; Change em back
                DONE

_       _       MINUS   101 8
                MINUS   102 8
                MINUS   103 8
                GFX     100 9           ; Next RGB values
                SKIP    $FADE

